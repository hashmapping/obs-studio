#include "srgb_legacy.inc"

uniform float4x4 ViewProj;
uniform texture2d image;

uniform float4 color;
uniform float contrast;
uniform float brightness;
uniform float gamma;

uniform float4 key_color;
uniform float similarity;
uniform float smoothness;

sampler_state textureSampler {
	Filter    = Linear;
	AddressU  = Clamp;
	AddressV  = Clamp;
};

struct VertData {
	float4 pos : POSITION;
	float2 uv  : TEXCOORD0;
};

VertData VSDefault(VertData v_in)
{
	VertData vert_out;
	vert_out.pos = mul(float4(v_in.pos.xyz, 1.0), ViewProj);
	vert_out.uv  = v_in.uv;
	return vert_out;
}

float4 CalcColor(float4 rgba)
{
	return float4(pow(rgba.rgb, float3(gamma, gamma, gamma)) * contrast + brightness, rgba.a);
}

float GetColorDist(float3 rgb)
{
	return distance(key_color.rgb, rgb);
}

float4 ProcessColorKey(float4 rgba)
{
	float colorDist = GetColorDist(rgba.rgb);
	rgba.a *= saturate((colorDist - similarity) / smoothness);

	return CalcColor(rgba);
}

float4 PSColorKeyRGBA(VertData v_in) : TARGET
{
	float4 sample = image.Sample(textureSampler, v_in.uv);
	float4 sample_srgb = float4(ConvertToSrgbNonlinear(sample.rgb), sample.a);
	float4 rgba = sample_srgb * color;
	float4 keyed = ProcessColorKey(rgba);
	return float4(ConvertFromSrgbNonlinear(keyed.rgb), keyed.a);
}

technique Draw
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader  = PSColorKeyRGBA(v_in);
	}
}
